allprojects { subproject ->
    group = 'com.neva.osgi.toolkit'
    version = '1.0.0'

    repositories {
        mavenLocal()
        jcenter()
    }

    afterEvaluate {

        plugins.withId 'org.jetbrains.kotlin.jvm', {
            compileKotlin {
                kotlinOptions {
                    jvmTarget = "1.8"
                }
            }
            compileTestKotlin {
                kotlinOptions {
                    jvmTarget = "1.8"
                }
            }
        }

        plugins.withId 'java', {

            jar {
                manifest {
                    attributes([
                            'Implementation-Title'  : project.description,
                            'Implementation-Version': project.version
                    ])
                }
            }
        }

        plugins.withId 'com.jfrog.bintray', {
            bintray {
                user = project.properties.get('bintrayUser') ?: System.getenv('BINTRAY_USER')
                key = project.properties.get('bintrayKey') ?: System.getenv('BINTRAY_KEY')
                publications = ['mavenJava']
                pkg {
                    repo = 'maven-public'
                    name = "osgi-toolkit-${subproject.name}"
                    userOrg = 'neva-dev'
                    licenses = ['Apache-2.0']
                    vcsUrl = 'https://github.com/neva-dev/gradle-osgi-toolkit.git'
                    labels = ['osgi', 'toolkit', 'felix', 'bundle']
                    version {
                        name = project.version
                        desc = "${project.description} ${project.version}"
                        vcsTag = project.version
                    }
                }
                publish = true
            }
        }

    }

}
